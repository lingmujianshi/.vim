"読み込み時の文字コード
set fileencodings=utf-8,cp932

"VI互換動作を無効
if &compatible
  set nocompatible
endif

" 一旦ファイルタイプ関連を無効化する
filetype off
filetype plugin indent off

"設定フォルダ
if has('win32')
    let g:DIR = expand('~/vimfiles')
else
    let g:DIR = expand('~/.vim')
endif

"設定ファイルを読み込む関数
function! s:source_rc(path, ...) abort "{{{
    let use_global = get(a:000, 0, !has('vim_starting'))
    let abspath = resolve(expand(g:DIR . '/rc/' . a:path))
    if !use_global
      execute 'source' fnameescape(abspath)
      return
    endif

    " substitute all 'set' to 'setglobal'
    let content = map(readfile(abspath),
       \ 'substitute(v:val, "^\\W*\\zsset\\ze\\W", "setglobal", "")')
    " create tempfile and source the tempfile
    let tempfile = tempname()
    try
        call writefile(content, tempfile)
        execute printf('source %s', fnameescape(tempfile))
    finally
        if filereadable(tempfile)
            call delete(tempfile)
        endif
    endtry
endfunction "}}}

"設定ファイルの読み込み
call s:source_rc('dein.rc.vim')
call s:source_rc('setting.vim')
call s:source_rc('capture.vim')

" ファイルタイプ関連を有効にする
filetype plugin indent on
syntax enable
